---
layout: post
title: A brief introduction to the Xfce Developer Tools
---


## Overview
 
The Xfce Developer Tools - <code>xfce4-dev-tools</code> for short - provide an
easy way to handle the setup and maintaince of a projects build framework. They
currently consist of a bunch of M4 macros for commonly used checks and the
<code>xdt-autogen</code> script, which examines the projects <code>configure.ac</code> 
or <code>configure.in</code> file(s) and calls the appropriate autotools in the
correct order.

As the name suggests, the Xfce Developer Tools are mainly useful for application
developers to maintain their build environment. But users will also be required
to install them if they plan to build software straight from CVS or SVN checkouts.


## Preparing the project

In this section I will describe how to use the developer tools with your project.
We will therefore examine a sample project and discuss the various steps. Our
sample project will contain only a single binary, which does nothing useful. The project
itself will be named <code>sample</code>, and will depend on <code>libxfcegui4</code> 
and can optionally use D-BUS (this is only to demonstrate the usage of some of
the M4 macros). And our project will be translatable to other languages.

We start off with the <code>Makefile.am</code>'s and the source code. First, we
create the projects directory layout:

```
$ mkdir sample
$ mkdir sample/po
$ mkdir sample/sample
```

Next the <code>sample/main.c</code> source file:

```c
#ifdef HAVE_CONFIG_H
#include <config.h>
#endif

#include <libxfcegui4/libxfcegui4.h>

#ifdef HAVE_DBUS
#include <dbus/dbus.h>
#endif

int
main (int argc, char **argv)
{
  xfce_textdomain (GETTEXT_PACKAGE, PACKAGE_LOCALE_DIR, "UTF-8");
  g_print (_("Hello World!\n"));
  return 0;
}
```

As you can see, we conditionally include <code>dbus/dbus.h</code> only if the
<code>HAVE_DBUS</code> preprocessor macro is defined. The check for D-BUS is
made in the <code>configure.ac</code> file, which is discussed below. Besides
that, the only thing we do here is to print the string <code>"Hello World!"</code> 
in a translatable manner, after setting up the proper textdomain for gettext.

We continue with the <code>sample/Makefile.am</code>, which contains instructions
about how to compile the <code>sample/main.c</code> file:

```make
INCLUDES = \
  -DDBUS_API_SUBJECT_TO_CHANGE \
  -DG_LOG_DOMAIN=\"sample\" \
  -DPACKAGE_LOCALE_DIR=\"$(localedir)\" \
  -I$(top_srcdir)

bin_PROGRAMS = \
  sample

sample_SOURCES = \
  main.c

sample_CFLAGS = \
  $(DBUS_CFLAGS) \
  $(LIBXFCEGUI4_CFLAGS)

sample_LDFLAGS = \
  $(DBUS_LIBS) \
  $(LIBXFCEGUI4_LIBS)
```

The <code>$(localedir)</code> make variable is set by the <code>XDT_I18N()</code> 
macro and will point to the location where the translations are installed (e.g.
<code>/usr/local/share/locale</code>). The <code>$(DBUS_CFLAGS)</code>,
<code>$(DBUS_LIBS)</code>, <code>$(LIBXFCEGUI4_CFLAGS)</code> and
<code>$(LIBXFCEGUI4_LIBS)</code> variables are set by the appropriate calls
to <code>XDT_CHECK_OPTIONAL_PACKAGE()</code> and <code>XDT_CHECK_PACKAGE()</code>,
which will be discussed below. Note that <code>$(DBUS_CFLAGS)</code> and
<code>$(DBUS_LIBS)</code> will be empty if D-BUS was not found or disabled
by the user.

We also need to define <code>DBUS_API_SUBJECT_TO_CHANGE</code> because the D-BUS
API is not yet frozen, but that's an unimportant detail in the context of this
article.

Furthermore we will also need a toplevel <code>Makefile.am</code>, which should
look like this:

```make
SUBDIRS = \
  po \
  sample

AUTOMAKE_OPTIONS = \
  1.8 \
  dist-bzip2
```

The <code>AUTOMAKE_OPTIONS</code> variable is used by <code>automake</code> to
enable or disable special behaviour. In this case, the <code>1.8</code> tells
<code>automake</code> that we need version 1.8 or better. And the <code>dist-bzip2</code> 
option tells <code>automake</code> to generate a <code>dist</code> target, which
will not only create the gzipped source tarball, but also a bzip2 compressed source
tarball.

Now we are done with the <code>Makefile.am</code>'s. The next step is to take care
of the i18n setup. Therefore we need to create an empty <code>po/ChangeLog</code> 
file (the <code>po/Makefile.in.in</code> generated by <code>glib-gettextize</code> 
requires this file)

```
$ touch po/ChangeLog
```

and afterwards create the <code>po/POTFILES.in</code> file, which lists all source
files (and sometimes other files like <code>.desktop</code> or <code>.xml</code> 
files as well) that contain translatable strings. In this case only the
<code>sample/main.c</code> file contains a translatable string, so the file
looks like this:

```
sample/main.c
```

Note that all paths in <code>po/POTFILES.in</code> must be relative to the projects
toplevel directory. One last step before we go on to the <code>configure.ac</code> 
file is to create four required files for GNU projects:

```
$ touch AUTHORS ChangeLog NEWS README
```

You should of course add content to these files later on, but for now, we want to
concentrate on the important facts, the contents of <code>configure.ac</code>:

```sh
dnl Version information
m4_define([sample_version_major], [0])
m4_define([sample_version_minor], [0])
m4_define([sample_version_micro], [1])
m4_define([sample_version], [sample_version_major().sample_version_minor().sample_version_micro()])

dnl Initialize autoconf
AC_COPYRIGHT([Copyright (c) 2005
Benedikt Meurer <benny@xfce.org>])
AC_INIT([sample], [sample_version()], [benny@xfce.org])

dnl Initialize automake
AM_INIT_AUTOMAKE([AC_PACKAGE_TARNAME()], [AC_PACKAGE_VERSION()])
AM_CONFIG_HEADER([config.h])
AM_MAINTAINER_MODE()

dnl check for basic programs
AC_PROG_CC()
AC_PROG_INSTALL()
AC_PROG_LIBTOOL()

dnl Check for i18n support
XDT_I18N([de])

dnl Check for required packages
XDT_CHECK_PACKAGE([LIBXFCEGUI4], [libxfcegui4-1.0], [4.2.0])

dnl Check for optional packages
XDT_CHECK_OPTIONAL_PACKAGE([DBUS], [dbus-1], [0.22], [dbus], [D-BUS support])

AC_OUTPUT([
Makefile
po/Makefile.in
sample/Makefile
])
```

You should be familar with most of the above, therefore we will only discuss the
<code>XDT_I18N()</code>, <code>XDT_CHECK_PACKAGE()</code> and
<code>XDT_CHECK_OPTIONAL_PACKAGE()</code> 
macros here. If you are not familar with the basic autoconf and automake macros,
you should have a look at the <a href="http://www.gnu.org/manual/manual.html">GNU
manuals</a>.


### XDT_I18N(LINGUAS [, PACKAGE])

This macro takes care of setting up everything for i18n support.

If <code>PACKAGE</code> isn't specified, it defaults to the package tarname; see
the description of <code>AC_INIT()</code> for an explanation of what makes up
the package tarname. Normally, you don't need to specify <code>PACKAGE</code>,
but you can stick with the default.


### XDT_CHECK_PACKAGE(VARNAME, PACKAGE, VERSION [, ACTION-IF [, ACTION-IF-NOT]])

Checks if <code>PACKAGE</code> >= <code>VERSION</code> is installed on the
target system, using the <code>pkg-config</code> utility. If the
dependency is met, <code>VARNAME_CFLAGS</code>, <code>VARNAME_LIBS</code>,
<code>VARNAME_VERSION</code> and <code>VARNAME_REQUIRED_VERSION</code> 
will be set and marked for substition. For example, if you specify <code>GTK</code> 
for the <code>VARNAME</code> parameter, the variables <code>GTK_CFLAGS</code>,
<code>GTK_LIBS</code>, <code>GTK_VERSION</code> and <code>GTK_REQUIRED_VERSION</code> 
will be set appropriately and substituted.

<code>VARNAME_REQUIRED_VERSION</code> will be set to the value of
<code>VERSION</code>. This is mostly useful to automatically
place the correct version information into the RPM <code>.spec</code> file.

In addition, if the dependency is met, <code>ACTION-IF</code> will
be executed if given.

If the package check fails, <code>ACTION-IF-NOT</code> will be
executed. If this parameter isn't specified, a diagnostic
message will be printed and the configure script will
be terminated with exit code 1.


### XDT_CHECK_OPTIONAL_PACKAGE(VARNAME, PACKAGE, VERSION, OPTIONNAME, HELPSTRING [, DEFAULT])

Checks for an optional dependency on <code>PACKAGE</code> >= <code>VERSION</code>.
<code>DEFAULT</code> can be <code>"yes"</code> or <code>"no"</code> (defaults to
<code>"yes"</code> if not specified) and controls whether configure should check this
dependency by default, or only if the user explicitly enables it using a command line switch.

This macro automatically adds a commandline switch based on the <code>OPTIONNAME</code> 
parameter (<code>--enable-optionname</code> and <code>--disable-optionname</code>), which
allows the user to explicitly control whether this optional dependency should be
enabled or not. The <code>HELPSTRING</code> parameter gives a brief(!) description
about this dependency.

If the user chose to enable this dependency and the required package
was found, this macro defines the variable <code>VARNAME_FOUND</code> and sets it
to the string <code>"yes"</code>, in addition to the 4 variables set by
<code>XDT_CHECK_PACKAGE()</code>.  But <code>VARNAME_FOUND</code> will not be marked
for substition. Furthermore, a C preprocessor define <code>HAVE_VARNAME</code> will be placed in
<code>config.h</code> (or added to the cc command line, depending on your <code>configure.ac</code> 
content) and set to 1.


## First run

Now that everything is in place we are ready for the first run. Therefore, go to the
toplevel directory of the project and execute the

```
$ xdt-autogen
```

command that comes with the developer tools. This will run all the required autotools (in our
case these are <code>glib-gettextize</code>, <code>libtoolize</code>, <code>aclocal</code>,
<code>autoheader</code>, <code>automake</code> and <code>autoconf</code>), and afterwards run
the generated <code>configure</code> script.

The next thing to do is to generate all language files specified with the <code>XDT_I18N()</code> 
macro (we've only specified <code>de</code> in our <code>configure.ac</code>). Therefore execute
the following commands:

```
$ cd po
$ make sample.pot
$ cp sample.pot de.po
```

Now if you check <code>po/de.po</code>, it will contain exactly one translatable string, the one
from <code>sample/main.c</code>.

That's it, now you can simply run

```
$ make
```

in the projects toplevel directory in order to build your project for the first time.
Congratulations, you've successfully completed the build framework setup.

The source code for the sample project is available
<a href="/files/source/xdt-introduction-sample.tar.gz">here</a>.

For more details on the internal workings and the other available macros, check the M4 files
in the <code>m4macros/</code> subdirectory of the Xfce Developer Tools distribution.


